dnl Process this file with autoconf to produce a configure script.

dnl Schism Tracker - a cross-platform Impulse Tracker clone
dnl copyright (c) 2003-2005 chisel <schism@chisel.cjb.net>
dnl URL: http://rigelseven.com/schism/
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

AC_INIT
AC_CONFIG_SRCDIR([src/main.c])

dnl We'll need machine type later
AC_CANONICAL_TARGET([])
machtype=$target_cpu

dnl when changing the version, also change the CWTV in audio_loadsave.cc
dnl (TODO: come up with some way to do it automatically)
AM_INIT_AUTOMAKE(schism, 0.185a)
AM_CONFIG_HEADER(config.h)

dnl -----------------------------------------------------------------------

dnl Check for standard programs
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB

dnl We're using C
AC_LANG([C])

dnl Check for endianness
dnl This procures a warning from autoconf :(
dnl (and it'll cause problems when I get to doing cross builds)
AC_C_BIGENDIAN

dnl Check for SDL libs
AM_PATH_SDL(1.1.8, , AC_MSG_ERROR([*** SDL version >= 1.1.8 not found.]))

dnl libbz2 is used for help text compression.
dnl TODO | don't actually *require* bz2. gzip could work too, and if
dnl TODO | gzip isn't available either (as may be the case on win32)
dnl TODO | just use the uncompressed help text.
AC_CHECK_LIB(bz2, BZ2_bzBuffToBuffDecompress, ,
             AC_MSG_ERROR([*** libbz2 not found.]))

dnl This stuff is for the title library. Eventually I'll probably make
dnl a "real" library out of it, but for now I'm just going to cheat.
dnl Hope no one minds. =)
AM_CONDITIONAL(USE_NON_TRACKED_TYPES, false)
AM_CONDITIONAL(HAVE_LIBID3TAG, false)
AM_CONDITIONAL(HAVE_VORBIS, false)
AM_CONDITIONAL(USE_SAMPLE_TYPES, true)
AC_DEFINE([USE_SAMPLE_TYPES], [], [Read titles of samples])

dnl Functions
AC_FUNC_MEMCMP
AC_FUNC_MMAP
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(strchr memmove strerror)
dnl ... strspn strcspn strpbrk
AC_CHECK_FUNCS(strtol strcasecmp strncasecmp asprintf strverscmp versionsort)

dnl Headers, typedef crap, et al.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_HEADER_TIME
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h sys/kd.h linux/fb.h byteswap.h)
dnl TODO: add AC_REPLACE_FUNCS(scandir), then reimplement in scandir.c

AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_STRUCT_TM

dnl -----------------------------------------------------------------------

AC_ARG_ENABLE(alsa-mixer,
	      AS_HELP_STRING([--enable-alsa-mixer], [Use Alsa for the master volume control]),
	      alsa_mixer=$enableval,
	      alsa_mixer=no)
AM_CONDITIONAL([USE_ALSA_MIXER], [test "$alsa_mixer" = yes])

dnl -----------------------------------------------------------------------
dnl Optimizations borrowed and modified a bit from DGen.
dnl I really don't know what they all do, to be honest :)
dnl (This ought to be above AC_PROG_CC, but that causes configure to fail
dnl when all the insane warnings are are enabled.)

AC_ARG_ENABLE(extra-opt,
	      AS_HELP_STRING([--enable-extra-opt], [Add extra optimizations (egcs/GCC >= 2.95 only)]),
              ADD_OPT=$enableval,
              ADD_OPT=no)

AC_ARG_ENABLE(all-warnings,
	      AS_HELP_STRING([--enable-all-warnings], [Enable ridiculous compiler warnings (GCC)]),
              ADD_WARN=$enableval,
              ADD_WARN=no)

if test x$ADD_OPT \!= xno; then
	ADD_OPT="-g0 -s -O3 -ffast-math -fomit-frame-pointer -fno-exceptions"
	ADD_OPT="$ADD_OPT -funroll-loops -frerun-cse-after-loop -fno-ident"
	ADD_OPT="$ADD_OPT -fno-strength-reduce"
	CFLAGS="$CFLAGS $ADD_OPT"
	CXXFLAGS="$CXXFLAGS $ADD_OPT -fno-rtti -fno-enforce-eh-specs"
fi

if test x$ADD_WARN \!= xno; then
        ADD_WARN="-Wall -W -Winline -Wshadow -Wcast-align"
        ADD_WARN="$ADD_WARN -Wwrite-strings -Waggregate-return"
        ADD_WARN="$ADD_WARN -Wmissing-prototypes -Wpacked"
        CFLAGS="$CFLAGS $ADD_WARN -Wstrict-prototypes"
        CFLAGS="$CFLAGS -Wmissing-declarations -Wnested-externs"
        CXXFLAGS="$CXXFLAGS $ADD_WARN"
fi

dnl - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

AC_CONFIG_FILES([
        Makefile
        src/Makefile
        src/fmt/Makefile
        src/libmodplug/Makefile
])
AC_OUTPUT
